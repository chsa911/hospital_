plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.6'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.google.protobuf' version '0.9.4'
}

repositories { mavenCentral() }

ext {
    grpcVersion = '1.62.2'
    protobufVersion = '3.25.3'
}

tasks.named('compileJava') {
    dependsOn 'generateProto'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "net.devh:grpc-server-spring-boot-starter:2.15.0.RELEASE"

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    // for @javax.annotation.Generated in stubs on Java 11+
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" } }
    generateProtoTasks {
        all().each { t -> t.plugins { grpc {} } }
    }
}

sourceSets {
    main {
        proto { srcDir 'src/main/proto' }
        java {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
    }
}
